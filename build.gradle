buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

// Default parameters
if (!project.hasProperty("nexusUrl")) {
    ext.nexusUrl = "file:///tmp/sdkRepo"
}

if (!project.hasProperty("nexusUsername")) {
    ext.nexusUsername = ""
}

if (!project.hasProperty("nexusPassword")) {
    ext.nexusPassword = ""
}

ext.nexusRelease = "${nexusUrl}/service/local/staging/deploy/maven2/"
ext.nexusSnapshot = "${nexusUrl}/content/repositories/snapshots/"

ext.commonLicense = {
    name 'The GNU General Public License (GPL)'
    url 'https://www.gnu.org/licenses/gpl-2.0.txt'
}

ext.devs = [
        {
            id 'aveuiller'
            name 'Antoine Veuiller'
            email 'aveuiller@gmail.com'
        }
]

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'maven'
    apply plugin: 'signing'

    group = "io.apisense"
    version = "1.0"
}

private static String[] toRelease(String repository) {
    def result = []
    for (String toRelease : ["RhinoEngine"]) {
        result.add(":publish" + toRelease + "PublicationTo" + repository + "Repository")
    }
    return result
}

