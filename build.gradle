buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

apply from: 'nexusConfig.gradle'

ext.commonLicense = {
    name 'The GNU General Public License (GPL)'
    url 'https://www.gnu.org/licenses/gpl-2.0.txt'
}

ext.devs = [
        {
            id 'aveuiller'
            name 'Antoine Veuiller'
            email 'aveuiller@gmail.com'
        }
]

private boolean signingConfigured() {
    return project.hasProperty("signing.keyId") && project.hasProperty("signing.password") \
     && project.hasProperty("signing.secretKeyRingFile")
}

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'maven'
    if (signingConfigured()) {
        apply plugin: 'signing'
        signing {
            sign configurations.archives
        }
    }

    group = "io.apisense"
    version = "1.1-SNAPSHOT"
}

private static String[] toRelease(String repository) {
    def result = []
    for (String toRelease : ["RhinoEngine"]) {
        result.add(":publish" + toRelease + "PublicationTo" + repository + "Repository")
    }
    return result
}

